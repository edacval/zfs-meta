# Maintainer <edacval at gmail dot com>

_default_branch=0.7-release
_kernelname=${_kernelname:-}
_kpkgver=${_kpkgver:-$(pacman -Sd --print-format '%v' linux-headers${_kernelname})}

case ${_buildtype} in
    branch)
        _branch=${_branch:-${_default_branch}}
        _zfsrev=${_zfsrev:-zfs-${_branch}}
        ;;
    *)
        _zfscommit=${_zfscommit:-origin/HEAD}
        _zfsrev=${_zfsrev:-${_zfscommit}}
        _buildtype=commit
        ;;
esac

pkgname=("zfs-headers-shared-git")
pkgver=0.7.11
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=("zfs::git+https://github.com/zfsonlinux/zfs.git#${_buildtype}=${_zfsrev}")
makedepends=("git" "linux${_kernelname}-headers=${_kpkgver}")

pkgver() {
    local ztag=$(git --git-dir="${srcdir}/zfs/.git" describe --tags --abbrev=0)
        printf "%s.%s" \
        "$(echo $ztag | sed 's/^zfs-//g; s/-//g')" \
        "$(git --git-dir="${srcdir}/zfs/.git" rev-list ${ztag}..HEAD --count --)"
}

prepare() {
    # Remove unwanted or useless features
    sed -i -e '/^SUBDIRS/ s/ rpm//' \
        "${srcdir}/zfs/Makefile.am"
}

build() {
    # After (2018-07-30) commit https://git.archlinux.org/svntogit/packages.git/commit/repos/core-x86_64?h=packages/linux&id=973e9461f3f2a558bfc49f49d659ea3f7e5aec44
    # kernel modules path cannot be guessed from linux${_kernelname}-headers package version so ugly hack:
    _kmoddir=${_kmoddir:-$(< $(pacman -Qlq linux${_kernelname}-headers | grep '/kernel\.release$'))}

    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
}

package_zfs-headers-shared-git() {
    pkgdesc="Zettabyte File System shared kernel headers"
    license=("CDDL")
    conflicts=("zfs-headers-shared")
    provides=( "zfs-headers-shared")
    conflicts=("spl-headers-shared")
    provides=( "spl-headers-shared")
    cd "${srcdir}/zfs/include"
    make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')
