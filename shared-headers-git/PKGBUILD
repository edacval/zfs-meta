# Maintainer <edacval at gmail dot com>

_default_tag=2.2.3
_tag=${_tag:-${_default_tag}}
_default_commit=origin/HEAD
_zfsrev=${_zfsrev:-${_default_commit}}

_kernelname=${_kernelname:-}
_kpkgver=${_kpkgver:-$(pacman -Sdd --print-format '%v' linux${_kernelname}-headers)}

pkgname=("zfs-kernel-headers-shared-git")
pkgver=${_tag}
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
        "zfs::git+https://github.com/zfsonlinux/zfs.git#commit=${_zfsrev}"
       )
makedepends=(
        "git"
        "linux${_kernelname}-headers=${_kpkgver}"
        "python"
        )

pkgver() {
    cd zfs
    local ztag=$(git describe --tags --abbrev=0)
    printf "%s.%s" \
        "$(echo $ztag | sed 's/^zfs-//; s/-//g')" \
        "$(git rev-list ${ztag}..HEAD --count --)"
}

prepare() {
    # Remove unwanted or useless features
    sed -i \
        -e '/^include config\/deb.am/d' \
        -e '/^include config\/rpm.am/d' \
        -e '/^include config\/tgz.am/d' \
        -e '/^include $(srcdir)\/%D%\/tests\/Makefile.am/d' \
        "${srcdir}/zfs/Makefile.am"
    sed -i -E \
        -e '/^\s+contrib\/debian\/rules$/d' \
        -e '/^\s+rpm\/.+$/d' \
        -e '/^\s+tests\/.+$/d' \
        "${srcdir}/zfs/configure.ac"
    sed -i \
        -e '/^include $(srcdir)\/%D%\/initramfs\/Makefile.am/d' \
        "${srcdir}/zfs/contrib/Makefile.am"
}

build() {
    _kmoddir=${_kmoddir:-$(< $(pacman -Qlq linux${_kernelname}-headers | grep '/kernel\.release$'))}

    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
}

package_zfs-kernel-headers-shared-git() {
    pkgdesc="Zettabyte File System shared kernel headers"
    license=("CDDL" "GPL2")
    conflicts=("zfs-kernel-headers-shared")
    provides=("zfs-kernel-headers-shared")
    depends=("zfs-utils=${_tag}")

    cd "${srcdir}/zfs/include"
    make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP')
