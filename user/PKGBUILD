# Maintainer <edacval at gmail dot com>

_default_tag=0.7.12
_tag=${_tag:-${_default_tag}}
_zfsrev=${_zfsrev:-zfs-${_tag}}
_splrev=${_splrev:-spl-${_tag}}

pkgname=("zfs-utils" "spl-utils")
pkgver=0.7.12
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
    "zfs::git+https://github.com/zfsonlinux/zfs.git#tag=${_zfsrev}"
    "spl::git+https://github.com/zfsonlinux/spl.git#tag=${_splrev}"
    "zfs-utils.initcpio.hook"
    "zfs-utils.initcpio.install"
    "zfs-utils.initcpio.zfsencryptssh.install"
    "zfs-utils.bash-completion-r1")
makedepends=("git")

pkgver() {
    printf "%s" "${_tag}"
}

prepare() {
    # Remove unwanted or useless features
    sed -i \
        -e '/^SUBDIRS/ s/ contrib//' \
        -e '/^SUBDIRS/ s/ rpm//' \
        -e '/^SUBDIRS/ s/ tests//' \
        "${srcdir}/zfs/Makefile.am" \
        "${srcdir}/spl/Makefile.am"
    # use python2
    sed -i '1s/python\b/python2/' \
        "${srcdir}/zfs/cmd/arc_summary/arc_summary.py"
}

build() {
    cd "${srcdir}/spl"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-config=user
    make
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --enable-systemd=yes \
        --enable-sysvinit=no \
        --with-mounthelperdir=/usr/bin \
        --with-udevdir=/usr/lib/udev \
        --with-config=user
    make
}

package_spl-utils() {
    pkgdesc="Solaris Porting Layer userspace tools"
    license=("GPL")
    optdepends=("python: monitoring tools")
    conflicts=("spl-utils")
    provides=("spl-utils")

    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install
}

package_zfs-utils() {
    pkgdesc="Zettabyte File System userspace tools"
    license=("CDDL")
    optdepends=("python2: monitoring tools"
                "sudo: zpool status/iostat disk monitoring as unprivileged user"
                "smartmontools: zpool status/iostat disk monitoring")
    conflicts=("zfs-utils")
    provides=("zfs-utils")

    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install
    # Install the support files
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.zfsencryptssh.install "${pkgdir}"/usr/lib/initcpio/install/zfsencryptssh
    install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
    chmod 0750  "${pkgdir}"/etc/sudoers.d
}


sha256sums=('SKIP'
            'SKIP'
            '3411316aefd5363957888597516445b9fa1f5506fe9c8e0e296eb93bb898b24a'
            'e785c4b2b6dc06e4db9cc5638236c93c962804c50cadd86e1e6e2acdf05dfc0d'
            '29080a84e5d7e36e63c4412b98646043724621245b36e5288f5fed6914da5b68'
            'b60214f70ffffb62ffe489cbfabd2e069d14ed2a391fac0e36f914238394b540')
