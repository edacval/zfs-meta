# Maintainer <edacval at gmail dot com>

_default_tag=2.2.7
_tag=${_tag:-${_default_tag}}
_zfsrev=${_zfsrev:-zfs-${_tag}}

pkgname=("zfs-utils")
pkgver=${_tag}
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
        "zfs::git+https://github.com/zfsonlinux/zfs.git#tag=${_zfsrev}"
        "zfs-utils.initcpio.hook"
        "zfs-utils.initcpio.install"
        "zfs-utils.initcpio.zfsencryptssh.install"
       )
makedepends=(
        "libaio"
        "git"
        "python"
        "python-cffi"
        "python-setuptools"
        )

backup=('etc/zfs/zed.d/zed.rc')

prepare() {
    # Remove unwanted or useless features
    sed -i \
        -e '/^include config\/deb.am/d' \
        -e '/^include config\/rpm.am/d' \
        -e '/^include config\/tgz.am/d' \
        -e '/^include $(srcdir)\/%D%\/tests\/Makefile.am/d' \
        "${srcdir}/zfs/Makefile.am"
    sed -i -E \
        -e '/^\s+contrib\/debian\/rules$/d' \
        -e '/^\s+rpm\/.+$/d' \
        -e '/^\s+tests\/.+$/d' \
        "${srcdir}/zfs/configure.ac"
    sed -i \
        -e '/^include $(srcdir)\/%D%\/initramfs\/Makefile.am/d' \
        "${srcdir}/zfs/contrib/Makefile.am"
}

build() {
    # Disable tree vectorization. Related issues:
    # https://github.com/openzfs/zfs/issues/13605
    # https://github.com/openzfs/zfs/issues/13620
    export CFLAGS="$CFLAGS -fno-tree-vectorize"
    export CXXFLAGS="$CXXFLAGS -fno-tree-vectorize"

    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --enable-nls=no \
        --enable-pyzfs=yes \
        --enable-systemd=yes \
        --enable-sysvinit=no \
        --without-libunwind \
        --with-mounthelperdir=/usr/bin \
        --with-udevdir=/usr/lib/udev \
        --with-config=user
    make bashcompletiondir=/usr/share/bash-completion/completions
}

package_zfs-utils() {
    pkgdesc="Zettabyte File System userspace tools"
    license=("CDDL" "GPL2")
    depends=(
        "libaio"
        )
    optdepends=(
            "python: monitoring tools,pyzfs"
            "sudo: zpool status/iostat disk monitoring as unprivileged user"
            "smartmontools: zpool status/iostat disk monitoring"
            )

    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" bashcompletiondir=/usr/share/bash-completion/completions install

    # Install the support files
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.zfsencryptssh.install "${pkgdir}"/usr/lib/initcpio/install/zfsencryptssh
    chmod 0750  "${pkgdir}"/etc/sudoers.d
}


sha256sums=('75a031bf85844d0f58fcb92e4280b18392593121676a57a68a38c28add363255'
            '74a20ba343e85ec50fa477e302dcb9a6f1c9262744f5d6df40afdb5231d4235b'
            'c72e4cc0124df32cf28739c46238d4d2b9571b6f2f84558e105429583d8968eb'
            'e069800906a9069f0eed01f95639ab7ff80ed69b6ebe30b4df21c1639d13a868')
