# Maintainer <edacval at gmail dot com>

_default_tag=0.8.6
_tag=${_tag:-${_default_tag}}
_zfsrev=${_zfsrev:-zfs-${_tag}}

pkgname=("zfs-utils")
pkgver=${_tag}
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
        "zfs::git+https://github.com/zfsonlinux/zfs.git#tag=${_zfsrev}"
        "zfs-utils.initcpio.hook"
        "zfs-utils.initcpio.install"
        "zfs-utils.initcpio.zfsencryptssh.install"
        "zfs-utils.bash-completion-r1"
        "autoconf.patch::https://github.com/openzfs/zfs/commit/3c7423946f6d44b49994e1d78385d15633ac20e6.diff"
        "gettext.patch"
       )
makedepends=(
        "git"
        "python"
        "python-cffi"
        "python-setuptools"
        )

prepare() {
    patch -N -p1 -d zfs -i "${srcdir}/autoconf.patch"
    patch -N -p1 -d zfs -i "${srcdir}/gettext.patch"
    # Remove unwanted or useless features
    sed -i \
        -e '/^SUBDIRS/ s/ rpm//' \
        -e '/^SUBDIRS/ s/ tests//' \
        "${srcdir}/zfs/Makefile.am"
    sed -i -E \
        -e '/^\s+contrib\/bash_completion\.d\/.+$/d' \
        -e '/^\s+contrib\/dracut\/.+$/d' \
        -e '/^\s+contrib\/initramfs\/.+$/d' \
        -e '/^\s+etc\/default\/.+$/d' \
        -e '/^\s+etc\/init\.d\/.+$/d' \
        -e '/^\s+rpm\/.+$/d' \
        -e '/^\s+tests\/.+$/d' \
        "${srcdir}/zfs/configure.ac"
    sed -i \
        -e '/^SUBDIRS/ s/ bash_completion.d//' \
        -e '/^SUBDIRS/ s/ dracut//' \
        -e '/^SUBDIRS/ s/ initramfs//' \
        "${srcdir}/zfs/contrib/Makefile.am"
    sed -i \
        -e '/^DIST_SUBDIRS/ s/ default//' \
        -e '/^DIST_SUBDIRS/ s/ init\.d//' \
        -e '/^SUBDIRS/ s/ default//' \
        -e '/^SUBDIRS/ s/ \${ZFS_INIT_SYSV)//' \
        "${srcdir}/zfs/etc/Makefile.am"
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --enable-systemd=yes \
        --enable-pyzfs=yes \
        --enable-sysvinit=no \
        --with-mounthelperdir=/usr/bin \
        --with-udevdir=/usr/lib/udev \
        --with-config=user
    make
}

package_zfs-utils() {
    pkgdesc="Zettabyte File System userspace tools"
    license=("CDDL" "GPL2")
    optdepends=(
            "python: monitoring tools,pyzfs"
            "sudo: zpool status/iostat disk monitoring as unprivileged user"
            "smartmontools: zpool status/iostat disk monitoring"
            )
    conflicts=("zfs-utils")
    provides=("zfs-utils")

    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install
    # Install the support files
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.zfsencryptssh.install "${pkgdir}"/usr/lib/initcpio/install/zfsencryptssh
    install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
    chmod 0750  "${pkgdir}"/etc/sudoers.d
}


sha256sums=('SKIP'
            '3411316aefd5363957888597516445b9fa1f5506fe9c8e0e296eb93bb898b24a'
            '6d0d8144cad6046df8c19df1a8b9cdd80cbe96e080616a955f87f0f9359fe9b3'
            '29080a84e5d7e36e63c4412b98646043724621245b36e5288f5fed6914da5b68'
            'b60214f70ffffb62ffe489cbfabd2e069d14ed2a391fac0e36f914238394b540'
            'b11064ccd364f9abe587a15b9fe1e08e0e9a06b81b77accf93f6d927f315aa29'
            '255e6dc17d5bd15f3d2eb200b32d31d32f03f090df9367749877fa74440ed90d')
