# Maintainer <edacval at gmail dot com>

_default_tag=2.1.10
_tag=${_tag:-${_default_tag}}
_default_commit=origin/HEAD
_zfsrev=${_zfsrev:-${_default_commit}}

_kernelname=${_kernelname:-}
_alias=${_kernelname:--ARCH}
_kpkgver=${_kpkgver:-$(pacman -Sdd --print-format '%v' linux${_kernelname}-headers)}

pkgname=(
        "zfs-linux${_alias}-git"
        "zfs-linux${_alias}-headers-git"
        )
pkgver=${_tag}
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
        "zfs::git+https://github.com/zfsonlinux/zfs.git#commit=${_zfsrev}"
       )
makedepends=(
        "git"
        "linux${_kernelname}-headers=${_kpkgver}"
        "python"
        )

pkgver() {
    cd zfs
    local ztag=$(git describe --tags --abbrev=0)
    printf "%s.%s.%s" "${_kpkgver/-/.}" \
        "$(echo $ztag | sed 's/^zfs-//; s/-//g')" \
        "$(git rev-list ${ztag}..HEAD --count --)"
}

prepare() {
    # Remove unwanted or useless features
    sed -i \
        -e '/^SUBDIRS/ s/ rpm//' \
        -e '/^SUBDIRS/ s/ tests//' \
        -e '/^include config\/deb.am/d' \
        -e '/^include config\/rpm.am/d' \
        "${srcdir}/zfs/Makefile.am"
    sed -i -E \
        -e '/^\s+contrib\/bash_completion\.d\/.+$/d' \
        -e '/^\s+contrib\/dracut\/.+$/d' \
        -e '/^\s+contrib\/initramfs\/.+$/d' \
        -e '/^\s+etc\/init\.d\/.+$/d' \
        -e '/^\s+include\/os\/freebsd\/.+$/d' \
        -e '/^\s+rpm\/.+$/d' \
        -e '/^\s+tests\/.+$/d' \
        "${srcdir}/zfs/configure.ac"
    sed -i \
        -e '/^DIST_SUBDIRS/ s/ bash_completion.d//' \
        -e '/^DIST_SUBDIRS/ s/ dracut//' \
        -e '/^DIST_SUBDIRS/ s/ initramfs//' \
        -e '/^SUBDIRS/ s/ bash_completion.d//' \
        -e '/^SUBDIRS/ s/ dracut//' \
        -e '/^SUBDIRS/ s/ initramfs//' \
        "${srcdir}/zfs/contrib/Makefile.am"
    sed -i \
        -e '/^DIST_SUBDIRS/ s/ init\.d//' \
        -e '/^SUBDIRS/ s/ $(ZFS_INIT_SYSV)//' \
        "${srcdir}/zfs/etc/Makefile.am"
}

build() {
    _kmoddir=${_kmoddir:-$(< $(pacman -Qlq linux${_kernelname}-headers | grep '/kernel\.release$'))}

    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
    make
    # Replace reference to ${srcdir} with proper path
    sed -i "s|${srcdir}/zfs/module/|/usr/lib/modules/${_kmoddir}/extra/|" \
        ${srcdir}/zfs/module/Module.symvers
}

_package-zfs-linux-git() {
    pkgdesc="Zettabyte File System kernel module for linux${_alias} kernel"
    license=("CDDL" "GPL2")
    depends=("kmod"
            "linux${_kernelname}=${_kpkgver}"
            "zfs-utils=${pkgver/#${_kpkgver/-/.}.}"
            )
    conflicts=("zfs-linux${_kernelname}")
    provides=( "zfs-linux${_kernelname}")

    cd "${srcdir}/zfs/module"
    make DESTDIR="${pkgdir}" DEPMOD=/bin/true INSTALL_MOD_PATH="${pkgdir}/usr" INSTALL_MOD_STRIP=1 install

    # Install mkinitcpio hook
    install -Dm644 /dev/null "${pkgdir}"/usr/lib/initcpio/zfs-linux${_alias}-git.trigger
}

_package-zfs-linux-headers-git() {
    pkgdesc="Zettabyte File System module headers for linux${_alias} kernel"
    license=("CDDL" "GPL2")
    conflicts=("zfs-linux${_kernelname}-headers")
    provides=("zfs-linux${_kernelname}-headers")
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install-nodist_kernelHEADERS
}

# Generate packaging functions on the fly - evil trick from kernel PKGBUILD
for _p in ${pkgname[@]}; do
    _tmp=${_p/${_alias}}
    eval "package_${_p}() {
        $(declare -f "_package-${_tmp}")
        _package-${_tmp}
    }"
done

sha256sums=('SKIP')
