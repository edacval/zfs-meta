# Maintainer <edacval at gmail dot com>

_default_tag=0.7.9
_kernelname=${_kernelname:-}
_alias=${_kernelname:--ARCH}
_kpkgver=${_kpkgver:-$(pacman -Sd --print-format '%v' linux${_kernelname}-headers)}

_tag=${_tag:-${_default_tag}}
_zfsrev=${_zfsrev:-zfs-${_tag}}
_splrev=${_splrev:-spl-${_tag}}

pkgname=("zfs-linux${_alias}"
         "spl-linux${_alias}"
         "zfs-linux${_alias}-headers"
         "spl-linux${_alias}-headers")
pkgver=4.17.11.1.0.7.9
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
    "zfs::git+https://github.com/zfsonlinux/zfs.git#tag=${_zfsrev}"
    "spl::git+https://github.com/zfsonlinux/spl.git#tag=${_splrev}"
    "zfs.hook")
makedepends=("git" "linux${_kernelname}-headers=${_kpkgver}")

pkgver() {
            printf "%s.%s" "${_kpkgver/-/.}" "${_tag}"
}

prepare() {
    # Remove unwanted or useless features
    sed -i -e '/^SUBDIRS/ s/ rpm//' \
        "${srcdir}/spl/Makefile.am" \
        "${srcdir}/zfs/Makefile.am"
}

build() {
    # After (2018-07-30) commit https://git.archlinux.org/svntogit/packages.git/commit/repos/core-x86_64?h=packages/linux&id=973e9461f3f2a558bfc49f49d659ea3f7e5aec44
    # kernel modules path cannot be guessed from linux${_kernelname}-headers package version so ugly hack:
    _kmoddir=${_kmoddir:-$(< $(pacman -Qlq linux${_kernelname}-headers | grep '/kernel\.release$'))}

    cd "${srcdir}/spl"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
    make
    # Replace reference to ${srcdir} with proper path
    sed -i "s|${srcdir}/spl/module/|/usr/lib/modules/${_kmoddir}/extra/|" \
        ${srcdir}/spl/module/Module.symvers
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-spl="${srcdir}/spl" \
        --with-spl-obj="${srcdir}/spl" \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
    make
    # Replace reference to ${srcdir} with proper path
    sed -i "s|${srcdir}/zfs/module/|/usr/lib/modules/${_kmoddir}/extra/|" \
        ${srcdir}/zfs/module/Module.symvers
}

_package-zfs-linux() {
    pkgdesc="Zettabyte File System kernel module for linux${_alias} kernel"
    license=("CDDL")
    depends=("kmod"
            "linux${_kernelname}=${_kpkgver}"
            "spl-linux${_alias}=$pkgver"
            "zfs-utils=${pkgver/#${_kpkgver/-/.}.}")
    conflicts=("zfs-linux${_kernelname}")
    provides=( "zfs-linux${_kernelname}")

    cd "${srcdir}/zfs/module"
    make DESTDIR="${pkgdir}" DEPMOD=/doesnt/exist INSTALL_MOD_PATH="/usr" install

    # Install mkinitcpio hook
    install -Dm644 "${srcdir}"/zfs.hook \
        "${pkgdir}"/usr/share/libalpm/hooks/80-zfs-linux${_alias}.hook
    sed -i \
        -e "s/%ALIAS%/${_alias}/g" \
        -e "s/%KERNELNAME%/${_kernelname}/g" \
        "${pkgdir}"/usr/share/libalpm/hooks/80-zfs-linux${_alias}.hook
}

_package-spl-linux() {
    pkgdesc="Solaris Porting Layer linux${_alias} kernel module"
    license=("GPL")
    depends=("kmod"
            "linux${_kernelname}=${_kpkgver}"
            "spl-utils=${pkgver/#${_kpkgver/-/.}.}")
    conflicts=("spl-linux${_kernelname}")
    provides=( "spl-linux${_kernelname}")

    cd "${srcdir}/spl/module"
    make DESTDIR="${pkgdir}" INSTALL_MOD_PATH="/usr" install
}

_package-zfs-linux-headers() {
    pkgdesc="Zettabyte File System module headers for linux${_alias} kernel"
    license=("CDDL")
    conflicts=("zfs-linux${_kernelname}-headers")
    provides=("zfs-linux${_kernelname}-headers")
    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install-nodist_kernelHEADERS
}

_package-spl-linux-headers() {
    pkgdesc="Solaris Porting Layer module headers for linux${_alias} kernel"
    license=("GPL")
    conflicts=("spl-linux${_kernelname}-headers")
    provides=("spl-linux${_kernelname}-headers")
    cd "${srcdir}/spl"
    make DESTDIR="${pkgdir}" install-nodist_kernelHEADERS
}

# Generate packaging functions on the fly - evil trick from kernel PKGBUILD
for _p in ${pkgname[@]}; do
    _tmp=${_p/${_alias}}
    eval "package_${_p}() {
        $(declare -f "_package-${_tmp}")
        _package-${_tmp}
    }"
done

sha256sums=('SKIP'
            'SKIP'
            '18e79db53f386122638ee3cdeb6143571136bac88b179ab349de64d3320f0e28')
