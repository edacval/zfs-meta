# Maintainer <edacval at gmail dot com>

_default_branch=0.7-release
case ${_buildtype} in
    branch)
        _branch=${_branch:-${_default_branch}}
        _zfsrev=${_zfsrev:-zfs-${_branch}}
        ;;
    *)
        _zfscommit=${_zfscommit:-origin/HEAD}
        _zfsrev=${_zfsrev:-${_zfscommit}}
        _buildtype=commit
        ;;
esac

pkgname=("zfs-utils-git")
pkgver=0.7.0.1668
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
    "zfs::git+https://github.com/zfsonlinux/zfs.git#${_buildtype}=${_zfsrev}"
    "zfs-utils.initcpio.hook"
    "zfs-utils.initcpio.install"
    "zfs-utils.initcpio.zfsencryptssh.install"
    "zfs-utils.bash-completion-r1")
makedepends=("git" "python2-cffi" "python2-setuptools")

pkgver() {
    local ztag=$(git --git-dir="${srcdir}/zfs/.git" describe --tags --abbrev=0)
        printf "%s.%s" \
        "$(echo $ztag | sed 's/^zfs-//g; s/-//g')" \
        "$(git --git-dir="${srcdir}/zfs/.git" rev-list ${ztag}..HEAD --count --)"
}

prepare() {
    # Remove unwanted or useless features
    sed -i \
        -e '/^SUBDIRS/ s/ rpm//' \
        -e '/^SUBDIRS/ s/ tests//' \
        "${srcdir}/zfs/Makefile.am"
    sed -i \
        -e 's/ bash_completion.d//' \
        -e 's/ dracut//' \
        -e 's/ initramfs//' \
        "${srcdir}/zfs/contrib/Makefile.am"
    # use python2
    sed -i '1s/python\b/python2/' \
        "${srcdir}/zfs/cmd/arc_summary/arc_summary.py"
}

build() {
    cd "${srcdir}/zfs"
    ./autogen.sh
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --enable-systemd=yes \
        --enable-sysvinit=no \
        --with-mounthelperdir=/usr/bin \
        --with-udevdir=/usr/lib/udev \
        --with-config=user
    make
}


package() {
    pkgdesc="Zettabyte File System userspace tools"
    license=("CDDL")
    optdepends=("python2-cffi: pyzfs"
                "python2: monitoring tools"
                "sudo: zpool status/iostat disk monitoring as unprivileged user"
                "smartmontools: zpool status/iostat disk monitoring")
    conflicts=("zfs-utils")
    provides=("zfs-utils")
    conflicts=("spl-utils")
    provides=("spl-utils")

    cd "${srcdir}/zfs"
    make DESTDIR="${pkgdir}" install
    # Install the support files
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.hook "${pkgdir}"/usr/lib/initcpio/hooks/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.install "${pkgdir}"/usr/lib/initcpio/install/zfs
    install -D -m644 "${srcdir}"/zfs-utils.initcpio.zfsencryptssh.install "${pkgdir}"/usr/lib/initcpio/install/zfsencryptssh
    install -D -m644 "${srcdir}"/zfs-utils.bash-completion-r1 "${pkgdir}"/usr/share/bash-completion/completions/zfs
    chmod 0750  "${pkgdir}"/etc/sudoers.d
}


sha256sums=('SKIP'
            '9214c831e090493b41829293ff03120bdcaab2bebe63efd4714abcc18688e764'
            'e785c4b2b6dc06e4db9cc5638236c93c962804c50cadd86e1e6e2acdf05dfc0d'
            '29080a84e5d7e36e63c4412b98646043724621245b36e5288f5fed6914da5b68'
            'b60214f70ffffb62ffe489cbfabd2e069d14ed2a391fac0e36f914238394b540')
