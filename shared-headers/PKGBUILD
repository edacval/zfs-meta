# Maintainer <edacval at gmail dot com>

_default_tag=0.7.10
_kernelname=${_kernelname:-}
_kpkgver=${_kpkgver:-$(pacman -Sd --print-format '%v' linux-headers${_kernelname})}

_tag=${_tag:-${_default_tag}}
_zfsrev=${_zfsrev:-zfs-${_tag}}
_splrev=${_splrev:-spl-${_tag}}

pkgname=("zfs-headers-shared"
         "spl-headers-shared")
pkgver=0.7.10
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=("zfs::git+https://github.com/zfsonlinux/zfs.git#tag=${_zfsrev}"
        "spl::git+https://github.com/zfsonlinux/spl.git#tag=${_splrev}")
makedepends=("git" "linux${_kernelname}-headers=${_kpkgver}")

pkgver() {
            printf "%s" "${_tag}"
}

prepare() {
    # Remove unwanted or useless features
    sed -i -e '/^SUBDIRS/ s/ rpm//' \
        "${srcdir}/spl/Makefile.am" \
        "${srcdir}/zfs/Makefile.am"
}

build() {
    # After (2018-07-30) commit https://git.archlinux.org/svntogit/packages.git/commit/repos/core-x86_64?h=packages/linux&id=973e9461f3f2a558bfc49f49d659ea3f7e5aec44
    # kernel modules path cannot be guessed from linux${_kernelname}-headers package version so ugly hack:
    _kmoddir=${_kmoddir:-$(< $(pacman -Qlq linux${_kernelname}-headers | grep '/kernel\.release$'))}

    cd "${srcdir}/spl"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-spl="${srcdir}/spl" \
        --with-spl-obj="${srcdir}/spl" \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
}

package_zfs-headers-shared() {
    pkgdesc="Zettabyte File System shared kernel headers"
    license=("CDDL")
    conflicts=("zfs-headers-shared")
    provides=( "zfs-headers-shared")
    cd "${srcdir}/zfs/include"
    make DESTDIR="${pkgdir}" install
}

package_spl-headers-shared() {
    pkgdesc="Solaris Porting Layer shared kernel headers"
    license=("GPL")
    conflicts=("spl-headers-shared")
    provides=( "spl-headers-shared")
    cd "${srcdir}/spl/include"
    make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP'
            'SKIP')
