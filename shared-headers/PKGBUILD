# Maintainer <edacval at gmail dot com>

_default_tag=0.8.6
_tag=${_tag:-${_default_tag}}
_zfsrev=${_zfsrev:-zfs-${_tag}}

_kernelname=${_kernelname:-}
_kpkgver=${_kpkgver:-$(pacman -Sd --print-format '%v' linux${_kernelname}-headers)}

pkgname=("zfs-headers-shared")
pkgver=${_tag}
pkgrel=1
arch=("x86_64")
url="http://zfsonlinux.org/"
source=(
        "zfs::git+https://github.com/zfsonlinux/zfs.git#tag=${_zfsrev}"
        "autoconf.patch::https://github.com/openzfs/zfs/commit/3c7423946f6d44b49994e1d78385d15633ac20e6.diff"
        "gettext.patch"
       )
makedepends=(
        "git"
        "linux${_kernelname}-headers=${_kpkgver}"
        "python"
        )

prepare() {
    patch -N -p1 -d zfs -i "${srcdir}/autoconf.patch"
    patch -N -p1 -d zfs -i "${srcdir}/gettext.patch"
    # Remove unwanted or useless features
    sed -i \
        -e '/^SUBDIRS/ s/ rpm//' \
        -e '/^SUBDIRS/ s/ tests//' \
        "${srcdir}/zfs/Makefile.am"
    sed -i -E \
        -e '/^\s+contrib\/bash_completion\.d\/.+$/d' \
        -e '/^\s+contrib\/dracut\/.+$/d' \
        -e '/^\s+contrib\/initramfs\/.+$/d' \
        -e '/^\s+etc\/default\/.+$/d' \
        -e '/^\s+etc\/init\.d\/.+$/d' \
        -e '/^\s+rpm\/.+$/d' \
        -e '/^\s+tests\/.+$/d' \
        "${srcdir}/zfs/configure.ac"
    sed -i \
        -e '/^SUBDIRS/ s/ bash_completion.d//' \
        -e '/^SUBDIRS/ s/ dracut//' \
        -e '/^SUBDIRS/ s/ initramfs//' \
        "${srcdir}/zfs/contrib/Makefile.am"
    sed -i \
        -e '/^DIST_SUBDIRS/ s/ default//' \
        -e '/^DIST_SUBDIRS/ s/ init\.d//' \
        -e '/^SUBDIRS/ s/ default//' \
        -e '/^SUBDIRS/ s/ \${ZFS_INIT_SYSV)//' \
        "${srcdir}/zfs/etc/Makefile.am"
}

build() {
    _kmoddir=${_kmoddir:-$(< $(pacman -Qlq linux${_kernelname}-headers | grep '/kernel\.release$'))}

    cd "${srcdir}/zfs"
    ./autogen.sh
    ./configure --prefix=/usr \
        --libexecdir=/usr/lib \
        --localstatedir=/var \
        --sbindir=/usr/bin \
        --sysconfdir=/etc \
        --with-linux=/usr/lib/modules/${_kmoddir}/build \
        --with-linux-obj=/usr/lib/modules/${_kmoddir}/build \
        --with-config=kernel
}

package_zfs-headers-shared() {
    pkgdesc="Zettabyte File System shared kernel headers"
    license=("CDDL" "GPL2")
    conflicts=("zfs-headers-shared")
    provides=( "zfs-headers-shared")
    cd "${srcdir}/zfs/include"
    make DESTDIR="${pkgdir}" install
}

sha256sums=('SKIP'
            'b11064ccd364f9abe587a15b9fe1e08e0e9a06b81b77accf93f6d927f315aa29'
            '255e6dc17d5bd15f3d2eb200b32d31d32f03f090df9367749877fa74440ed90d')
